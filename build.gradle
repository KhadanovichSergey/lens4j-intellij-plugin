plugins {
    id 'java'
    id "io.freefair.lombok" version "6.2.0"
    id 'org.jetbrains.intellij' version '0.7.3'
    id "de.undercouch.download" version "4.1.2"
}

group 'dev.khbd.lens4j'
version '0.1.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'dev.khbd.lens4j', name: 'lens4j-core', version: '0.1.3'

    testImplementation group: 'org.testng', name: 'testng', version: '6.13.1'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.9.0'
}

intellij {
    version = System.getenv().getOrDefault('INTELLIJ_VERSION', intellij_version)
    plugins = ['com.intellij.java']
}

def loadHtml(String fileName) {
    file(fileName).text.replace('<html>', '').replace('</html>', '')
}

patchPluginXml {
    changeNotes = loadHtml("change-notes.html")
    pluginDescription = loadHtml("description.html")
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
    useTestNG()
}
