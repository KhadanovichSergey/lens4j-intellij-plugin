plugins {
    id 'java'
    id "io.freefair.lombok" version "6.2.0"
    id 'org.jetbrains.intellij' version '0.7.3'
    id "de.undercouch.download" version "4.1.2"
}

group 'dev.khbd.lens4j'
version(plugin_version + "_" + intellij_version)

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'dev.khbd.lens4j', name: 'lens4j-core', version: lens4j_version

    testImplementation group: 'org.testng', name: 'testng', version: '6.13.1'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.9.0'
}

intellij {
    version = intellij_version
    plugins = ['com.intellij.java']
}

def loadHtml(String fileName) {
    file(fileName).text.replace('<html>', '').replace('</html>', '')
}

patchPluginXml {
    changeNotes = loadHtml("change-notes.html")
    pluginDescription = loadHtml("description.html")
}

task libs(type: Sync) {
    from configurations.compileClasspath
    into "$buildDir/libs"
    include 'lens4j-core-' + lens4j_version + '.jar'
    rename 'lens4j-core-' + lens4j_version, 'lens4j'
}

def baseMockJdkLocation = "https://github.com/JetBrains/intellij-community/raw/master/java/mock"
def baseMockJdkDestination = "$buildDir/mock"

def downloadMockJdk(baseLocation, baseDestination, jdkVersion) {
    def location = baseLocation + jdkVersion
    def destination = baseDestination + jdkVersion

    download {
        src([
                "$location/jre/lib/annotations.jar",
                "$location/jre/lib/rt.jar"
        ])
        dest "$destination/jre/lib/"
        overwrite false
        quiet false
    }
}

task downloadMockJdk11() {
    downloadMockJdk(baseMockJdkLocation, baseMockJdkDestination, "JDK-11")
}

test.dependsOn(libs, downloadMockJdk11)

test {
    testLogging {
        exceptionFormat = 'full'
    }
    useTestNG()
}
